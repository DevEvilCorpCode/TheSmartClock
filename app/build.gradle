apply plugin: 'com.android.application'

/*
 * Copyright 2018 EvilCorpCode & original author Phillip Hsu
 *
 * This file is part of TheSmartClock.
 *
 * TheSmartClock is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * TheSmartClock is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with TheSmartClock.  If not, see <http://www.gnu.org/licenses/>.
 */

// =============================================================================
// https://developer.android.com/studio/publish/app-signing.html#secure-shared-keystore

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

// =============================================================================

android {
    signingConfigs {
        config {
            storeFile file(keystoreProperties['storeFile'])
            keyAlias keystoreProperties['keyAlias']
            storePassword keystoreProperties['storePassword']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId 'com.evilcorpcode.thesmartclock'
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        // Disabled for now because we're not ready to
        // completely port over to vector drawables
//        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            // https://developer.android.com/studio/build/shrink-code.html#shrink-code
            //
            // Proguard is disabled, because it seems like it is removing
            // ButterKnife generated code and I don't know how to fix it...
            minifyEnabled false
            // "'proguard-android-optimize.txt' includes the same ProGuard rules
            // [as 'proguard-android.txt'], but with other optimizations that
            // perform analysis at the bytecode level—inside and across methods—
            // to reduce your APK size further and help it run faster."
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config

            /*
            to make this work add in your gradle.properties the following:
            ADMOB_APP_ID="your_admob_app_id"
            AD_UNIT_HOME_BANNER_ID="your_ad_unit_id"
             */
            buildConfigField 'String', "ADMOB_APP_ID", ADMOB_APP_ID
            resValue 'string', "ADMOB_APP_ID", ADMOB_APP_ID

            buildConfigField 'String', "AD_UNIT_HOME_BANNER_ID", AD_UNIT_HOME_BANNER_ID
            resValue 'string', "AD_UNIT_HOME_BANNER_ID", AD_UNIT_HOME_BANNER_ID
        }
        debug {
            applicationIdSuffix ".debug"

            /*
            to make this work add in your gradle.properties the following:
            ADMOB_APP_ID="your_admob_app_id"
            AD_UNIT_HOME_BANNER_ID="your_ad_unit_id"
             */
            buildConfigField 'String', "ADMOB_APP_ID", ADMOB_APP_ID
            resValue 'string', "ADMOB_APP_ID", ADMOB_APP_ID

            buildConfigField 'String', "AD_UNIT_HOME_BANNER_ID", AD_UNIT_HOME_BANNER_ID
            resValue 'string', "AD_UNIT_HOME_BANNER_ID", AD_UNIT_HOME_BANNER_ID


        }
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    compileOnly 'com.google.auto.value:auto-value:1.2'
    annotationProcessor 'com.google.auto.value:auto-value:1.2'
    implementation 'com.google.android.gms:play-services-ads:11.6.0'
    //noinspection GradleCompatible,GradleCompatible
    implementation 'com.android.support:appcompat-v7:25.1.0'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:gridlayout-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation('com.philliphsu:bottomsheetpickers:2.3.2') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'gridlayout-v7'
    }
}
